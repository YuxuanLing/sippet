# Copyright (c) 2013-2017 The Sippet Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

use_v8_in_net = !is_ios && !is_android

static_library("sippet") {
  sources = [
    "message/message.h",
    "message/message.cc",
    "uri/uri.h",
    "uri/uri.cc",
    "uri/uri_canon.h",
    "uri/uri_canon.cc",
    "uri/uri_canon_internal.h",
    "uri/uri_canon_internal.cc",
    "uri/uri_parse.h",
    "uri/uri_parse.cc",
    "uri/uri_parse_internal.h",
    "uri/uri_util.h",
    "uri/uri_util.cc",
  ]

  public_deps = [
    "//base",
    "//net",
    ":build_version"
  ]

  deps = [
    "//url:url",
  ]

  if (is_ios) {
    # iOS has different user-agent construction utilities.
    sources -= [
      "base/user_agent_utils.cc",
    ]
  }
}

component("sippet_test_support") {
  testonly = true
  sources = [
    "transport/chrome/transport_test_util.h",
    "transport/chrome/transport_test_util.cc",
    "ua/auth_handler_mock.h",
    "ua/auth_handler_mock.cc",
  ]

  deps = [
    "//third_party/icu",
  ]

  public_deps = [
    "//net:net",
    "//net:test_support",
    "//testing/gtest",
    "//testing/gmock",
    ":sippet",
  ]
}

test("sippet_unittests") {
  sources = [
    "../net/test/run_all_unittests.cc",
    "message/message_unittest.cc",
    "message/headers_unittest.cc",
    "message/parser_unittest.cc",
    "uri/uri_unittest.cc",
    "transport/end_point_unittest.cc",
    "transport/network_layer_unittest.cc",
    "transport/chrome/chrome_datagram_writer_unittest.cc",
    "transport/chrome/chrome_stream_writer_unittest.cc",
    "ua/auth_controller_unittest.cc",
    "ua/auth_handler_digest_unittest.cc",
  ]

  deps = [
    ":sippet_test_support",
  ]

  if (use_v8_in_net) {
    deps += [
      "//mojo/edk/system",
    ]
  }
}

action("build_version") {
  script = "//sippet/build/sippet_version.py"
  lastchange = "//build/util/LASTCHANGE"
  template = "//sippet/build/sippet_version.h.in"
  generated = "$target_gen_dir/sippet_version.h"

  # Force recalculation if there's been a change.
  inputs = [
    script, lastchange, template,
  ]
  outputs = [
    generated,
  ]

  args = [
    "-f", rebase_path(lastchange),
    rebase_path(template),
    rebase_path(generated)
  ]
}
